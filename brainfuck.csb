#Brainfuck interpreter
#NOTE: The interpreter is VERY slow and doesn't have debug features.

0->DimZ
10->Z
Z->Dim

#Cells
1000->I
#Stack
10->Y

0->A
#Array location
1->B
#Program size
0->C
#Cursor location
#X
1->D
#Y
1->E
#Cell pointer
0->F
#Loop buffer
0->G
#Loop stack pointer
1->H

#Cleans input
For 0->X To 10:GetKey:Next
Lbl 0
	While A=0
		GetKey->A
	WhileEnd
	If B=Z:Then
		Z+1->Z
		Z->DimZ
	IfEnd
	A->Z[B]
	#+
	If A=77:Then
		Locate D,E,"+"
	IfEnd
	#-
	If A=67:Then
		Locate D,E,"-"
	IfEnd
	#> [x]
	If A=24:Then
		Locate D,E,"*"
	IfEnd
	#< [รท]
	If A=87:Then
		Locate D,E,"/"
	IfEnd
	#( [[]
	If A=43:Then
		Locate D,E,"["
	IfEnd
	#) []]
	If A=44:Then
		Locate D,E,"]"
	IfEnd
	#Input [,]
	If A=45:Then
		Locate D,E,","
	IfEnd
	#Output [.]
	If A=26:Then
		Locate D,E,"."
	IfEnd
	# [DEL]
	If A=34:Then
		0->Z[B]
		B-2->B
		D-1->D
		If D=0:Then
			If E>1:Then
				E-1->E
				16->D
			Else
				1->D
			IfEnd
		IfEnd
		Locate D,E," "
	IfEnd
	If A=77 Or A=67 Or A=24 Or A=87 Or A=43 Or A=44 Or A=26 Or A=45:Then
		D+1->D
		#Line wrap
		If D=16:Then
			1->D
			E+1->E
		IfEnd
		#New page
		If E=4 And D=15:Then
			Cls
			1->D
			1->E
		IfEnd
	IfEnd
	B+1->B
	If A!=47:Then
		0->A
		Goto 0
	IfEnd
	Cls
#Program size
Z->C
#Cell pointer
C+1->F
#Program size+Cells+Loop stack
Z+I+Y->Z
Z->DimZ
#Readin the program
For 1->B To C
	Lbl 2
		If Z[B]=43:Then
			If G=0:Then
				B->G
			Else
				G->Z[C+I+H]
				B->G
				H+1->H
			IfEnd
			If C+I+H=Z:Then
				#Error: Too many nested loops
				-1->H
				Goto 1
			IfEnd
		IfEnd
		If Z[B]=44:Then
			If G=0:Then
				#Missing [
				0->H
				Goto 1
			IfEnd
			If Z[F]=0:Then
				If Z[C+I+1]=0:Then
					0->G
					1->H
				Else
					Z[C+I+H-1]->G
					0->Z[C+I+H-1]
					H-1->H
				IfEnd
			Else
				G+1->B
				Goto 2
			IfEnd
		IfEnd
		If Z[B]=77:Then
			Z[F]+1->Z[F]
		IfEnd
		If Z[B]=67:Then
			Z[F]-1->Z[F]
		IfEnd
		If Z[B]=24:Then
			F+1->F
			If F>=C+I:Then
				Goto 1
			IfEnd
		IfEnd
		If Z[B]=87:Then
			If F=C:Then
				#Stop the overwriting of the program
				Goto 1
			Else
				F-1->F
			IfEnd
		IfEnd
		If Z[B]=26:Then
			Z[F]@
		IfEnd
		If Z[B]=45:Then
			?->Z[F]
		IfEnd
	Next
	0->Z
	Z->DimZ
	Stop
Lbl 1
#Error handling
	Cls
	Locate 1,1,"ERROR"
	#Missing ]
	If H=-1:Then
		Locate 1,2,")? L:"
		#Location of the error
		Locate 7,2,B
	IfEnd
	#Missing [
	If H=0:Then
		Locate 1,2,"(? L:"
		Locate 7,2,B
	IfEnd
	#Location of the character
	Locate 1,3,F
	Stop
#Var:A B C D E F G H I Y Z Z[]
#Goto: 0 1 2
